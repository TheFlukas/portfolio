#Область ОбработчикиСобытийФормы
 &НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)  
	
	Дата = ТекущаяДата();
	
	Ресторан = Справочники.Рестораны.ГлавныйРесторан();
		
	СоздатьИзмеренияПланировщика();
	
	ОбновитьПериодОтображенияПланировщика();
	
	ЗаполнитьЖурналЗаявок(); 
КонецПроцедуры
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Изменение_Заявки" Тогда
		ЗаполнитьЖурналЗаявок();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы
 &НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	     СтандартнаяОбработка = Ложь;
		 ЗначенияЗаполнения = Новый Структура; 
		 ЗначенияЗаполнения.Вставить("Ресторан", Ресторан);
		 ЗначенияЗаполнения.Вставить("Дата", Начало);
		 ЗначенияЗаполнения.Вставить("ДатаОкончания", Конец);
		 ЗначенияЗаполнения.Вставить("Сотрудник", ЗначенияИзмерений["Сотрудник"]); 
		 
		 СтруктураПараметров = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения); 
		 
		 ОткрытьФорму("Документ.ФормированиеЗаявки.ФормаОбъекта", СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	
	ЭлементПланировщика = ВыделенныеЭлементы[0];
	СтруктураПараметров = Новый Структура("Ключ", ЭлементПланировщика.Значение);
	ОткрытьФорму("Документ.ФормированиеЗаявки.ФормаОбъекта", СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриОкончанииРедактирования(Элемент, НовыйЭлемент, ОтменаРедактирования)
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	ЭлементПланировщика = ВыделенныеЭлементы[0];
	
	ЗначенияРеквизитов = Новый Структура;
	ЗначенияРеквизитов.Вставить("Дата", ЭлементПланировщика.Начало);
	ЗначенияРеквизитов.Вставить("ДатаОкончания", ЭлементПланировщика.Конец);
	ЗначенияРеквизитов.Вставить("Сотрудник", ЭлементПланировщика.ЗначенияИзмерений["Сотрудник"]);
	
	ОбновитьДанныеЗаявки(ЭлементПланировщика.Значение, ЗначенияРеквизитов);
	
	
	
КонецПроцедуры  

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	    ОбновитьПериодОтображенияПланировщика();
		ЗаполнитьЖурналЗаявок();
 КонецПроцедуры

&НаКлиенте
Процедура РесторанПриИзменении(Элемент)
	    ОбновитьПериодОтображенияПланировщика();
		ЗаполнитьЖурналЗаявок();
	КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ЗаполнитьЖурналЗаявок()  
	
	Планировщик.Элементы.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФормированиеЗаявки.Дата КАК Дата,
		|	ФормированиеЗаявки.Ресторан КАК Ресторан,
		|	ФормированиеЗаявки.Сотрудник КАК Сотрудник,
		|	ФормированиеЗаявки.Длительность КАК Длительность,
		|	ФормированиеЗаявки.Стол КАК Стол,
		|	ФормированиеЗаявки.Клиент КАК Клиент,
		|	ФормированиеЗаявки.Сумма КАК Сумма,
		|	ФормированиеЗаявки.Клиент.Телефон КАК Телефон,
		|	ФормированиеЗаявки.Клиент.Комментарий КАК Пожелания,
		|	ФормированиеЗаявки.Клиент.Email КАК EMail,
		|	ФормированиеЗаявки.ДатаОкончания КАК ДатаОкончания,
		|	ФормированиеЗаявки.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(ФормированиеЗаявки.Услуга) КАК Услуга,
		|	ФормированиеЗаявки.Состояние КАК Состояние,
		|	ФормированиеЗаявки.Важность КАК Важность
		|ИЗ
		|	Документ.ФормированиеЗаявки КАК ФормированиеЗаявки
		|ГДЕ
		|	ФормированиеЗаявки.Проведен
		|	И ФормированиеЗаявки.Ресторан = &Ресторан
		|	И ФормированиеЗаявки.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания";
	
	Запрос.УстановитьПараметр("Ресторан", Ресторан);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(Дата));
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	
	
	Пока Выборка.Следующий() Цикл
		
		ЗначенияИзмерений = Новый Соответствие;
		ЗначенияИзмерений.Вставить("Сотрудник", Выборка.Сотрудник);
			
		ЖирныйШрифт = Новый Шрифт(,,Истина);
		
		ПредставлениеКлиента = Строка(Выборка.Клиент); //+ " " + Выборка.Телефон; 
		МассивСтрок = Новый Массив;
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ПредставлениеКлиента, ЖирныйШрифт));
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Выборка.Телефон);
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(Выборка.Услуга);
		МассивСтрок.Добавить(Символы.ПС);
		Если ЗначениеЗаполнено(Выборка.Пожелания) Тогда
			МассивСтрок.Добавить("--" + Выборка.Пожелания);
		КонецЕсли;
		ЭлементПланировщика = Планировщик.Элементы.Добавить(Выборка.Дата, Выборка.ДатаОкончания); 
		ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(ЗначенияИзмерений);
		ЭлементПланировщика.Значение = Выборка.Ссылка;
		ЭлементПланировщика.Текст = Новый ФорматированнаяСтрока(МассивСтрок);
		Состояние = Выборка.Состояние.Получить();
		Если Состояние = Неопределено Тогда
			Состояние = webЦвета.ЗеленыйЛес;
		КонецЕсли;
		
		ЭлементПланировщика.ЦветФона = Состояние;
	КонецЦикла;	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПериодОтображенияПланировщика()

	Начало = НачалоДня(Дата) + 10*3600;
	Конец = НачалоДня(Дата) + 21*3600;
	
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(Начало, Конец);
КонецПроцедуры    

&НаСервере 
Процедура СоздатьИзмеренияПланировщика()
	  	Измерение = Планировщик.Измерения.Добавить("Сотрудник");   
		
		Выборка = Справочники.Сотрудники.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.ПометкаУдаления Тогда
				Продолжить;
			КонецЕсли;
		ЗначениеИзмерения = Измерение.Элементы.Добавить(Выборка.Ссылка);
		ЗначениеИзмерения.Текст = Выборка.Наименование + Символы.ПС + Выборка.Должность;
		ЗначениеИзмерения.ЦветРамки = WebЦвета.ЗамшаСветлый;
		ЗначениеИзмерения.ЦветФона = WebЦвета.Бежевый;   
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
	Процедура ОбновитьДанныеЗаявки(Заявка, ЗначенияРеквизитов)
	ОбъектЗаявки = Заявка.ПолучитьОбъект();
	ЕстьИзменения = Ложь;
	Для каждого Реквизит Из ЗначенияРеквизитов Цикл 
		Если ОбъектЗаявки[Реквизит.Ключ] <> Реквизит.Значение Тогда
			ЕстьИзменения = Истина;
			Прервать;
		КонецЕсли;	
	КонецЦикла;	
	
   Если ЕстьИзменения Тогда
		ЗаполнитьЗначенияСвойств(ОбъектЗаявки, ЗначенияРеквизитов);
		ОбъектЗаявки.Длительность = (ОбъектЗаявки.ДатаОкончания - ОбъектЗаявки.Дата) / 3600; 
		ОбъектЗаявки.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти


