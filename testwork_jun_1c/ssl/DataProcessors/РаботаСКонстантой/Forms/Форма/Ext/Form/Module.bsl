&НаКлиенте
Функция ПроверитьВыбранТипДанных() Экспорт
	Если ПустаяСтрока(ЭтаФорма.ТипДанныхСодержимогоКонстанты) Тогда
		ВывестиСообщение("Выберите тип данных для константы перед выполнением операции.");
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

&НаСервере
Процедура ОбновитьКонстантуПлатформ()
	
	ЗапросПлатформ = Новый Запрос;
	ЗапросПлатформ.Текст =
	"ВЫБРАТЬ
	|	ПлСсылки.Ссылка КАК Платформа
	|ИЗ
	|	Справочник.Платформы КАК ПлСсылки";
	
	Результат = ЗапросПлатформ.Выполнить();
	ТаблицаПлатформ = Результат.Выгрузить();
	
	СохранитьПлатформыВКонстанту(ТаблицаПлатформ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКонстанту(Команда)
	Если НЕ ПроверитьВыбранТипДанных() Тогда Возврат; КонецЕсли;
	
	УдалитьРеквизитыПлатформ();
	ОбновитьКонстантуПлатформ();
	ОчиститьСообщения();
	ВывестиСообщение("Константа успешно обновлена");
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПлатформы(Команда)
	Если НЕ ПроверитьВыбранТипДанных() Тогда Возврат; КонецЕсли;
	
	ОчиститьСообщения();
	ПоказатьПлатформыИзКонстанты();	
КонецПроцедуры

&НаСервере
Процедура ПоказатьПлатформыИзКонстанты()
	
	УдалитьРеквизитыПлатформ();
	ТЗСсылки = ПолучитьСсылкиИзКонстанты();
	
	Если ТЗСсылки = Неопределено Или ТЗСсылки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВТ.Платформа КАК Платформа
	|ПОМЕСТИТЬ ВТ_Отображение
	|ИЗ
	|	&ВТ КАК ВТ
	|;
	|ВЫБРАТЬ
	|	ВТ_Отображение.Платформа КАК Платформа
	|ИЗ
	|	ВТ_Отображение КАК ВТ_Отображение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Платформы КАК Пл
	|		ПО ВТ_Отображение.Платформа = Пл.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	Пл.ЧисловойКод УБЫВ";
	
	Запрос.УстановитьПараметр("ВТ", ТЗСсылки);
	Результат = Запрос.Выполнить();
	Отсортированные = Результат.Выгрузить();
	
	СоздатьРеквизитИТаблицу(Отсортированные);
	
	НоваяСтрока = ЭтаФорма.КлючевыеПлатформы.Добавить();
	Индекс = 0;
	Для Каждого Пл Из Отсортированные Цикл
		Индекс = Индекс + 1;
		НоваяСтрока["Платформа" + Индекс] = Пл.Платформа;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНеКлючевые(Команда) 
	Если НЕ ПроверитьВыбранТипДанных() Тогда Возврат; КонецЕсли;
	ОчиститьСообщения();
	УдалитьНеКлючевыеНаСервере();
КонецПроцедуры

&НаСервере
Процедура УдалитьНеКлючевыеНаСервере()     
	
	тзИсходныеСсылкиИзКонстанты = ПолучитьСсылкиИзКонстанты(); 
	
	Если тзИсходныеСсылкиИзКонстанты = Неопределено Тогда 
		ВывестиСообщение("Недопустимый тип данных константы. Выполнение прервано."); 
		Возврат;    
	КонецЕсли;   
	
	// Получаем только ключевые платформы, упорядоченные по убыванию числового кода
	Запрос = Новый Запрос; 
	Запрос.Текст = 
	"ВЫБРАТЬ
	|   ВТ_СсылкиПлатформ.Платформа КАК Платформа
	|ПОМЕСТИТЬ ВТ_СсылкиПлатформИзКонстанты
	|ИЗ
	|   &ВТ_СсылкиПлатформ КАК ВТ_СсылкиПлатформ
	|;
	|
	|ВЫБРАТЬ
	|   ВТ_СсылкиПлатформИзКонстанты.Платформа КАК Платформа
	|ИЗ
	|   ВТ_СсылкиПлатформИзКонстанты КАК ВТ_СсылкиПлатформИзКонстанты
	|   ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Платформы КАК Платформы
	|   ПО ВТ_СсылкиПлатформИзКонстанты.Платформа = Платформы.Ссылка
	|ГДЕ
	|   Платформы.Ключевая
	|УПОРЯДОЧИТЬ ПО
	|   Платформы.ЧисловойКод УБЫВ";
	
	
	Запрос.УстановитьПараметр("ВТ_СсылкиПлатформ", тзИсходныеСсылкиИзКонстанты);
	РезультатЗапроса = Запрос.Выполнить();      
	тзКлючевые = РезультатЗапроса.Выгрузить();
	
	// Сохраняем ключевые платформы обратно в константу
	СохранитьПлатформыВКонстанту(тзКлючевые); 
	
	// Отображаем обновлённые платформы на форме
	ПоказатьПлатформыИзКонстанты();
	
	// Выводим сообщение об удалённых платформах
	ВывестиСообщениеОбУдаленныхПлатформах(тзИсходныеСсылкиИзКонстанты);  
	
КонецПроцедуры



&НаСервере
Процедура СоздатьРеквизитИТаблицу(ТЗПлатформы)
	
	Реквизиты = Новый Массив;
	РеквизитТаблицы = Новый РеквизитФормы("КлючевыеПлатформы", Новый ОписаниеТипов("ТаблицаЗначений"), , "Ключевые платформы");
	Реквизиты.Добавить(РеквизитТаблицы);
	
	Индекс = 0;
	Для Каждого Пл Из ТЗПлатформы Цикл
		Индекс = Индекс + 1;
		РеквизитПлатформы = Новый РеквизитФормы("Платформа" + Индекс, Новый ОписаниеТипов("СправочникСсылка.Платформы"), "КлючевыеПлатформы", "Платформа" + Индекс);
		Реквизиты.Добавить(РеквизитПлатформы);
	КонецЦикла;
	
	ЭтаФорма.ИзменитьРеквизиты(Реквизиты);
	
	ЭлементТаблицы = Элементы.Добавить("КлючевыеПлатформы", Тип("ТаблицаФормы"));
	ЭлементТаблицы.ПутьКДанным = "КлючевыеПлатформы";
	ЭлементТаблицы.КоманднаяПанель.Видимость = Ложь;
	
	Индекс = 0;
	Для Каждого Пл Из ТЗПлатформы Цикл
		Индекс = Индекс + 1;
		ЭлементПоля = Элементы.Добавить("Платформа" + Индекс, Тип("ПолеФормы"), ЭлементТаблицы);
		ЭлементПоля.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементПоля.ПутьКДанным = "КлючевыеПлатформы.Платформа" + Индекс;
		ЭлементПоля.ГиперссылкаЯчейки = Истина;
		ЭлементПоля.ТолькоПросмотр = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьРеквизитыПлатформ()
	
	Элемент = Элементы.Найти("КлючевыеПлатформы");
	Если Элемент <> Неопределено Тогда
		Элементы.Удалить(Элемент);
		МассивУдаляемых = Новый Массив;
		МассивУдаляемых.Добавить("КлючевыеПлатформы");
		ЭтаФорма.ИзменитьРеквизиты(, МассивУдаляемых);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиСообщениеОбУдаленныхПлатформах(тзИсходныеСсылкиИзКонстанты)     
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|   ВТ_СсылкиПлатформ.Платформа КАК Ссылка
	|ПОМЕСТИТЬ ВТ_СсылкиПлатформИзКонстанты
	|ИЗ
	|   &ВТ_СсылкиПлатформ КАК ВТ_СсылкиПлатформ
	|;
	|
	|ВЫБРАТЬ
	|   ЕСТЬNULL(Платформы.Наименование, ""Не найдено наименование"") КАК Наименование,
	|   ЕСТЬNULL(Платформы.ЧисловойКод, 0) КАК ЧисловойКод
	|ИЗ
	|   ВТ_СсылкиПлатформИзКонстанты КАК ВТ_СсылкиПлатформИзКонстанты
	|   ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Платформы КАК Платформы
	|   ПО ВТ_СсылкиПлатформИзКонстанты.Ссылка = Платформы.Ссылка
	|ГДЕ
	|   ЕСТЬNULL(Платформы.Ключевая, ЛОЖЬ) = ЛОЖЬ
	|УПОРЯДОЧИТЬ ПО
	|   ЧисловойКод"; 
	
	Запрос.УстановитьПараметр("ВТ_СсылкиПлатформ", тзИсходныеСсылкиИзКонстанты); 
	РезультатЗапроса = Запрос.Выполнить();      
	тзНеКлючевые = РезультатЗапроса.Выгрузить(); 
	
	Если тзНеКлючевые.Количество() > 0 Тогда  
		мНеКлючевые = тзНеКлючевые.ВыгрузитьКолонку("Наименование");   
		ВывестиСообщение("Удалено платформ: " + мНеКлючевые.Количество() + " (" 
		+ СтрСоединить(мНеКлючевые, ", ") + ").");    
	Иначе                                   
		ВывестиСообщение("В константе нет неключевых платформ, ничего не удалено"); 
	КонецЕсли; 
	
КонецПроцедуры


&НаСервере
Процедура СохранитьПлатформыВКонстанту(ТЗПлатформы)
	
	ТипКонстанты = ТипДанныхСодержимогоКонстанты;
	ЗначениеДляКонстанты = Неопределено;
	
	Если ТипКонстанты = "Строка" Тогда
		МассивGUID = Новый Массив;
		Для Каждого Строка Из ТЗПлатформы Цикл
			Если ТипЗнч(Строка.Платформа) = Тип("СправочникСсылка.Платформы") Тогда
				МассивGUID.Добавить(Строка.Платформа.УникальныйИдентификатор());
			КонецЕсли;
		КонецЦикла;
		ЗначениеДляКонстанты = СтрСоединить(МассивGUID, ";");
		
	ИначеЕсли ТипКонстанты = "Массив" Тогда
		ЗначениеДляКонстанты = ТЗПлатформы.ВыгрузитьКолонку("Платформа");
		
	Иначе
		// Для ТаблицаЗначений или любого другого типа сохраняем как есть
		ЗначениеДляКонстанты = ТЗПлатформы;
	КонецЕсли;
	
	Хранилище = Новый ХранилищеЗначения(ЗначениеДляКонстанты);
	
	Попытка
		Константы.ПлатформыРезерв.Установить(Хранилище);
	Исключение
		ВывестиСообщение("Не удалось записать значения в константу ПлатформыРезерв");
		ЗаписьЖурналаРегистрации("Ошибка записи в константу",
		УровеньЖурналаРегистрации.Информация,
		Метаданные.Константы.ПлатформыРезерв,
		,"Не удалось записать значения в константу");
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСсылкиИзКонстанты()

    Попытка
        ХЗ = Константы.ПлатформыРезерв.Получить();
    Исключение
        ВывестиСообщение("Константа ПлатформыРезерв недоступна.");
        Возврат Неопределено;
    КонецПопытки;
    
    Значение = ХЗ.Получить();
    
    // --- Проверка на допустимые типы ---
    Допустимые = Новый Массив;
    Допустимые.Добавить(Тип("Строка"));
    Допустимые.Добавить(Тип("Массив"));
    Допустимые.Добавить(Тип("ТаблицаЗначений"));
    
    Если Допустимые.Найти(ТипЗнч(Значение)) = Неопределено Тогда
        ВывестиСообщение("Тип данных в константе не поддерживается: " + ТипЗнч(Значение));
        Возврат Неопределено;
    КонецЕсли;
    
    // --- Строка GUID ---
    Если ТипЗнч(Значение) = Тип("Строка") Тогда
        Возврат ПреобразоватьСтрокуGUIDВТЗ(Значение);
    КонецЕсли;
    
    // --- Массив ---
    Если ТипЗнч(Значение) = Тип("Массив") Тогда
        
        ТЗ = Новый ТаблицаЗначений;
        ТЗ.Колонки.Добавить("Платформа", Новый ОписаниеТипов("СправочникСсылка.Платформы"));
        
        Для Каждого Элемент Из Значение Цикл
            Если ТипЗнч(Элемент) = Тип("СправочникСсылка.Платформы") И НЕ Элемент.Пустая() Тогда
                Строка = ТЗ.Добавить();
                Строка.Платформа = Элемент;
            КонецЕсли;
        КонецЦикла;
        
        Возврат ТЗ;
        
    КонецЕсли;
    
    // --- ТаблицаЗначений ---
    Если ТипЗнч(Значение) = Тип("ТаблицаЗначений") Тогда
        
        // Проверка наличия колонки через цикл
        КолонкаЕсть = Ложь;
        Для Каждого Кол Из Значение.Колонки Цикл
            Если Кол.Имя = "Платформа" Тогда
                КолонкаЕсть = Истина;
                Прервать;
            КонецЕсли;
        КонецЦикла;
        
        Если НЕ КолонкаЕсть Тогда
            ВывестиСообщение("В таблице значений отсутствует колонка 'Платформа'.");
            Возврат Неопределено;
        КонецЕсли;
        
        // копируем, убирая битые ссылки
        ТЗ = Новый ТаблицаЗначений;
        ТЗ.Колонки.Добавить("Платформа", Новый ОписаниеТипов("СправочникСсылка.Платформы"));
        
        Для Каждого Строка Из Значение Цикл
            Если ТипЗнч(Строка.Платформа) = Тип("СправочникСсылка.Платформы") И НЕ Строка.Платформа.Пустая() Тогда
                НовСтрока = ТЗ.Добавить();
                НовСтрока.Платформа = Строка.Платформа;
            КонецЕсли;
        КонецЦикла;
        
        Возврат ТЗ;
        
    КонецЕсли;
    
    Возврат Неопределено;

КонецФункции


&НаСервереБезКонтекста
Функция ПреобразоватьСтрокуGUIDВТЗ(СтрокаGUID)
	
	Если ПустаяСтрока(СтрокаGUID) Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	МассивGUID = СтрРазделить(СтрокаGUID, ";");
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Платформа", Новый ОписаниеТипов("СправочникСсылка.Платформы"));
	
	Для Каждого GUID Из МассивGUID Цикл
		Если ПустаяСтрока(СокрЛП(GUID)) Тогда Продолжить; КонецЕсли;
		СтрокаТЗ = ТЗ.Добавить();
		Попытка
			СтрокаТЗ.Платформа = XMLЗначение(Тип("СправочникСсылка.Платформы"), СокрЛП(GUID));
		Исключение
			ТЗ.Удалить(СтрокаТЗ);
			ЗаписьЖурналаРегистрации("Ошибка GUID", УровеньЖурналаРегистрации.Информация, Метаданные.Константы.ПлатформыРезерв,,,"Некорректный GUID: " + GUID);
		КонецПопытки;
	КонецЦикла;
	
	Возврат ТЗ;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиСообщение(Текст)
	
	Сообщ = Новый СообщениеПользователю;
	Сообщ.Текст = Текст;
	Сообщ.Сообщить();
	
КонецПроцедуры
